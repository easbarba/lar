#! /usr/bin/env elixir

defmodule Dots do
  defp ignored(path, {:ok, files}) do
    File.stream!(files)
    |> Enum.map(&String.trim(&1))
    |> Enum.concat([".dotsignore"])
    |> Enum.map(&Path.join(path, &1))
    |> MapSet.new()
  end

  defp ignored(_path, {:error, nil}) do
    []
  end

  defp ignored_exist?(path) do
    path = Path.join(path, ".dotsignore")
    if File.exists?(path), do: {:ok, path}, else: {:error, nil}
  end

  defp ignore_me?(path, item) do
    ignored(path, ignored_exist?(path))
    |> Enum.any?(&String.starts_with?(item, &1))
  end

  def ls_r(path) do
    cond do
      File.regular?(path) ->
        [path]

      File.dir?(path) ->
        File.ls!(path)
        |> Enum.map(&Path.join(path, &1))
        |> Enum.map(&ls_r/1)
        |> Enum.concat()

      true ->
        []
    end
  end

  def walk(path) do
    items = ls_r(path)

    for item <- items do
      unless ignore_me?(path, item) do
        target = item
        link_name = to_home(item, path)

        make_folder(link_name)
        link_file(target, link_name)
      end
    end
  end

  def to_home(item, path) do
    # /data/dots/.config/mpd/mpd.conf to $HOME/.config/mpd/mpd.conf
    String.replace(item, path, System.user_home())
  end

  def make_folder(link_name) do
    link_dir = Path.dirname(link_name)

    unless File.exists?(link_dir) do
      File.mkdir_p!(link_dir)
    end
  end

  def link_file(target, link_name) do
    unless File.exists?(link_name) do
      IO.puts("#{target} -> #{link_name}")
      File.ln_s!(target, link_name)
    end
  end

  def deploy(path) do
    walk(path)
  end

  def pretend(path) do
    IO.puts("pretend-mode")
    IO.inspect(path)
  end

  def info(path) do
    IO.puts("root: #{path}")
  end
end

defmodule CLI do
  def parse(args) do
    args
    |> OptionParser.parse(
      switches: [deploy: :string, pretend: :string, help: :boolean],
      aliases: [D: :deploy, P: :pretend, H: :help]
    )
    |> elem(0)
    |> run()
  end

  def help do
    IO.puts("Usage: dots [options]
  -D, --deploy                    symlink all dotfiles
  -P, --pretend                   pretend to symlink all dotfiles
  -H, --help                      cli options information")

    System.halt(0)
  end

  def run(deploy: path) do
    path = Path.dirname(IO.chardata_to_string(path))
    Dots.deploy(path)
  end

  def run(pretend: path) do
    path = Path.dirname(IO.chardata_to_string(path))
    Dots.pretend(path)
  end

  def run(help: true) do
    help()
  end

  def run(_) do
    help()
  end
end

CLI.parse(System.argv())
